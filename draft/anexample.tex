\section{The essence of external testing}
\label{sec:essence}

In the present section, we illustrate the essential idea of external testing
through a simple example, which is that of a module \code{SIList} whose type
\code{t} represents sorted integer lists. The invariant is maintained by making
\code{t} abstract and requiring the user to go through the exported functions
\code{empty} and \code{add}.

This section, unfolding from the initial example, introduces the key ideas of
external testing: a GADT type that describes well-typed applications in the
simply-typed lambda calculus; a description of module signatures that we wish to
test; type descriptors that record all the instances of a type that we managed
to construct.

The point of view adopted in this section is intentedly simplistic. The design,
as presented here, contains several obvious shortcomings. It allows,
nonetheless, for a high-level overview of our principles, and paves the way for
a more thorough discussion of our design which appears in the subsequent section
\sref{sec:representation}.

Here is the signature for our module of sorted integer lists.
%
\begin{ocamlcode}
module type SIList = sig
  type t

  val empty: t
  val add: t -> int -> t
  val check: t -> bool
end
\end{ocamlcode}
%
The \code{check} function represents the \emph{invariant} that the module
pretends it preserves. The module admits a straightforward implementation, as
follows.
%
\begin{ocamlcode}
module SIList = struct
  type t = int list

  let empty = []

  let rec add x = function
    | [] -> [x]
    | t::q -> if t<x then t::add x q else x::t::q

  let rec check = function
    | [] | [_] -> true
    | t1::(t2::_ as q) -> t1 <= t2 && check q
end
\end{ocamlcode}
%
Roughly speaking, our goal is to generate, as if we were \emph{client code} of
the module, instances of type \code{t} using only the functions exported by the
module. Therefore, one of our first requirements is a data structure for keeping
track of the \code{t}'s created so far.
%
\begin{ocamlcode}
type 'a ty = {
  (* Other implementation details omitted *)
  mutable enum: 'a list;
  fresh: ('a list -> 'a) option;
}
\end{ocamlcode}
%
A type descriptor \code{'a ty} keeps track of all the \emph{instances} we have
created so far in its \code{enum} field. Built-in types such as \code{int}
do not belong to the set of types whose invariants we wish to check. For such
types, we provide a \code{fresh} function that generates an instance different
from all that we have generated so far.

From the point of view of the client code, all we can do is combine
\code{add} and \code{empty} to generate new instances. \arti, as a fake client,
should thus behave similarly and automatically perform repeated applications of
\code{add} so as to generate new instances. We thus need a description of what
combinations of functions are legal for \arti to perform.

In essence, we want to represent well-typed applications in the
simply-typed lambda-calculus. This can be embedded in OCaml using generalized
algebraic data types (GADTs). We define the GADT \code{('f, 'r) fn}, which
describes ways to generate instances of type \code{'r} using a function of type
\code{'f}. We call it a \emph{function descriptor}.
%
\begin{ocamlcode}
type (_,_) fn =
| Ret: 'a ty -> ('a,'a) fn
| Fun: 'a ty * ('b, 'c) fn -> ('a -> 'b, 'c) fn

(* Helpers for creating [fn]'s. *)
let (@->) ty fd = Fun (ty,fd)
let returning ty = Constant ty
\end{ocamlcode}
%
The \code{Ret} case describes a constant value, which has type \code{'a} and
produce one instance of type \code{'a}. For reasons that will soon become
apparent, we also record the descriptor of type \code{'a}. \code{Fun} describes
the case of a function from \code{'a} to \code{'b}: using the descriptor of type
\code{'a}, we can apply the function to obtain instances of type \code{'b};
combining that with the other \code{('b, 'c) fn} gives us a way to produce
elements of type \code{'c}, hence the \code{('a -> 'b, 'c) fn} conclusion.
%
\begin{ocamlcode}
let (>>=) li f = List.flatten (List.map f li)

let rec eval : type a b. (a,b) fn -> a -> b list =
  fun fd f ->
    match fd with
    | Ret _ -> [f]
    | Fun (ty,fd) ->
        ty.enum >>= fun e -> eval fd (f e)

let rec codom : type a b. (a,b) fn -> b ty =
  function
    | Fun (_,fd) -> codom fd
    | Ret ty -> ty
\end{ocamlcode}
%
The \code{eval} function is central: taking a function descriptor \code{fd}, it
recurses over it, thus refining the type of its argument \code{f}. The use of
GADTs allows us to statically prove that the \code{eval} function only ever
produces instances of type \code{b}.
%
The \code{codom} function allows one to find the type
descriptor associated to the return value (the codomain) of an \code{fn}.

Using the two functions above, it then becomes trivial to generate new instances
of \code{'b}.
%
\begin{ocamlcode}
let use (fd: ('a, 'b) fn) (f: 'a) =
  let prod = eval fd f in
  let ty = codom fd in
  List.iter (fun x -> 
    if mem x ty then () else ty.enum <- x::ty.enum
  ) prod
\end{ocamlcode}
%
The function takes a function descriptor along with a matching function. The
\code{prod} variable contains all the instances of \code{'b} we just managed to
create; \code{ty} is the descriptor of \code{'b}. We store the new
instances of \code{'b} in the corresponding type descriptor.

In order to wrap this up nicely, one can define \emph{signature descriptors}. An
entry in a signature descriptor is merely a function of a certain
type \code{'a} along with its corresponding function descriptor. Once this is
done, the user can finally call our library and test the functions found in the
signature description.
%
\begin{ocamlcode}
type sig_elem = Elem : ('a,'b) fn * 'a -> elem
type sig_descr = (string * sig_elem) list
let si_t  =
  (* create a descriptor for [SIList.t]... *)
let int_t =
  (* ...and one for [int], with a [fresh] function *)

let sig_of_silist = [
  ("empty", (returning si_t, SIList.empty));
  ("add", (int_t @-> si_t @-> returning si_t, SIList.add));
]

let _ =
  Arti.check sig_of_silist SIList.check
\end{ocamlcode}
%
The \code{Arti.check} function repeatedly calls \code{use} on the items found in
the signature, until the desired number of instances have been created. The
library calls \code{use} for each function in the signature several times:
failing that, the only applications we could ever build would be of the form
\code{add empty n}. The library then fetches the descriptor for \code{SIList.t}
and check that each instance satisfies the \code{SIList.check} predicate.
