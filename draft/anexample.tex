\section{The essence of external testing}
\label{sec:essence}

In the present section, we illustrate the essential idea of external testing
through a simple example, which is that of a module \code{SIList} whose type
\code{t} represents sorted integer lists. The invariant is maintained by making
\code{t} abstract and requiring the user to go through the exported functions
\code{empty} and \code{add}.

This section, unfolding from the initial example, introduces the key ideas of
external testing: a GADT type that describes well-typed applications in the
simply-typed lambda calculus; a description of module signatures that we wish to
test; type descriptors that record all the instances of a type that we managed
to construct.

The point of view adopted in this section is intendedly simplistic. The design,
as presented here, contains several obvious shortcomings. It allows,
nonetheless, for a high-level overview of our principles, and paves the way for
a more thorough discussion of our design (\sref{sec:representation}).

Here is the signature for our module of sorted integer lists.
The \code{check} function represents the \emph{invariant} that the module
intends to preserve. The module admits a straightforward implementation, which
we also show.
%
% 7dd765501e4d12d98e9d90a0ece490486cfe6913
\begin{ocamlcode}
module type SIList: sig
  type t
  val empty: t
  val add: t -> int -> t
  val check: t -> bool
end = struct
  type t = int list
  let empty = []
  let rec add x = function
    | [] -> [x]
    | t::q -> if t<x then t::add x q else x::t::q
  let rec check = function
    | [] | [_] -> true
    | t1::(t2::_ as q) -> t1 <= t2 && check q
end
\end{ocamlcode}
%
Roughly speaking, our goal is to generate, as if we were \emph{client code} of
the module, instances of type \code{t} using only the functions exported by the
module. Therefore, one of our first requirements is a data structure for keeping
track of the \code{t}'s created so far. We also need to keep track of the
integers we have generated so far, since they are necessary to call the
\code{add} function: \arti will thus manipulate several \code{ty}'s for all the
types it handles.
%
\begin{ocamlcode}
type 'a ty = { (* Other implementation details omitted *)
  mutable enum: 'a list;
  fresh: ('a list -> 'a) option; }
\end{ocamlcode}
%
A type descriptor \code{'a ty} keeps track of all the \emph{instances} of
\code{'a} we have created so far in its \code{enum} field. Built-in types such
as \code{int} do not belong to the set of types whose invariants we wish to
check. For such types, we provide a \code{fresh} function that generates an
instance different from all that we have generated so far.

From the point of view of the client code, all we can do is combine
\code{add} and \code{empty} to generate new instances. \arti, as a fake client,
should thus behave similarly and automatically perform repeated applications of
\code{add} so as to generate new instances. We thus need a description of what
combinations of functions are legal for \arti to perform.

In essence, we want to represent well-typed applications in the
simply-typed lambda-calculus. This can be embedded in OCaml using generalized
algebraic data types (GADTs). We define the GADT \code{('a, 'b) fn}, which
describes ways to generate instances of type \code{'b} using a function of type
\code{'a}. We call it a \emph{function descriptor}.
%
\begin{ocamlcode}
type (_,_) fn =
| Ret: 'a ty -> ('a,'a) fn
| Fun: 'a ty * ('b, 'c) fn -> ('a -> 'b, 'c) fn
(* Helpers for creating [fn]'s. *)
let (@->) ty fd = Fun (ty,fd)
let returning ty = Ret ty
\end{ocamlcode}
%
The \code{Ret} case describes a constant value, which has type \code{'a} and
produce one instance of type \code{'a}. For reasons that will soon become
apparent, we also record the descriptor of type \code{'a}. \code{Fun} describes
the case of a function from \code{'a} to \code{'b}: using the descriptor of type
\code{'a}, we can apply the function to obtain instances of type \code{'b};
combining that with the other \code{('b, 'c) fn} gives us a way to produce
elements of type \code{'c}, hence the \code{('a -> 'b, 'c) fn} conclusion.
%
\begin{ocamlcode}
let rec eval : type a b. (a,b) fn -> a -> b list =
  fun fd f ->
    match fd with
    | Ret _ -> [f]
    | Fun (ty,fd) -> List.flatten (
        List.map (fun e -> eval fd (f e)) ty.enum)
let rec codom : type a b. (a,b) fn -> b ty =
  function
    | Ret ty -> ty
    | Fun (_,fd) -> codom fd
\end{ocamlcode}
%
The \code{eval} function is central: taking a function descriptor \code{fd}, it
recurses over it, thus refining the type of its argument \code{f}. The use of
GADTs allows us to statically prove that the \code{eval} function only ever
produces instances of type \code{b}.
%
The \code{codom} function allows one to find the type
descriptor associated to the return value (the codomain) of an \code{fn}.

Using the two functions above, it then becomes trivial to generate new instances
of \code{'b}.
%
% The function below doesn't need the special GADT syntax. Go figure.
\begin{ocamlcode}
let use (fd: ('a, 'b) fn) (f: 'a): () =
  let prod = eval fd f in
  let ty = codom fd in
  List.iter (fun x -> 
    if mem x ty then () else ty.enum <- x::ty.enum
  ) prod
\end{ocamlcode}
%
The function takes a function descriptor along with a matching function. The
\code{prod} variable contains all the instances of \code{'b} we just managed to
create; \code{ty} is the descriptor of \code{'b}. We store the new
instances of \code{'b} in the corresponding type descriptor.

In order to wrap this up nicely, one can define \emph{signature descriptors}. An
entry in a signature descriptor is merely a function of a certain
type \code{'a} along with its corresponding function descriptor. Once this is
done, the user can finally call our library and test the functions found in the
signature description.
%
\begin{ocamlcode}
type sig_elem = Elem : ('a,'b) fn * 'a -> sig_elem
type sig_descr = (string * sig_elem) list
let si_t  = (* create a descriptor for [SIList.t] and... *)
let int_t = (* one for [int], with a [fresh] function *)
let sig_of_silist = [
  ("empty", (returning si_t, SIList.empty));
  ("add", (int_t @-> si_t @-> returning si_t, SIList.add)); ]
let _ =
  Arti.generate sig_of_silist;
  assert (Arti.counter_example si_t SIList.check = None)
\end{ocamlcode}
%
The \code{Arti.generate} function repeatedly calls \code{use} on the items found in
the signature, until the desired number of instances have been created. We need
to perform several rounds of calls; with a single pass, the only applications we
would ever build would be of the form \code{add empty n}. We then check that the
\code{SIList.check} predicate holds for all the \code{SIList.t} generated.
